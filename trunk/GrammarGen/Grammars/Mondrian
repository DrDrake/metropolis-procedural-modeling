
// Macros defining rules:

// Symbol stores: size x, y, n (subdivision)


///////////////////////////////////////////////////////////////////////////////////////

DerivationLength: 30;
Axiom : Color(1,1,1) Rect(10,10,6);

///////////////////////////////////////////////////////////////////////////////////////

#define ChooseSplitRatio(prob,ratio) \
Rect1(x,y,n,type) <prob> -> Rect2(x,y,n,type,ratio)


//ChooseSplitRatio(1/36,1/12); 
ChooseSplitRatio(1/36,1/10); 
//ChooseSplitRatio(1/24,1/7); 
ChooseSplitRatio(1/18,1/5); 
//ChooseSplitRatio(1/12,1/3); 
//ChooseSplitRatio(1/10,2/5); 
ChooseSplitRatio(1/8,1/2); 
ChooseSplitRatio(1/10,3/5); 
//ChooseSplitRatio(1/12,2/3); 
//ChooseSplitRatio(1/18,4/5); 
//ChooseSplitRatio(1/24,6/7); 
ChooseSplitRatio(1/24,1/1); 

// choose the split or color
Rect(x,y,n) <0.4> : n > 0 && y > x/5 -> Rect1(x,y,n-1,1); // vertical
Rect(x,y,n) <0.4> : n > 0 && x > y/5 -> Rect1(x,y,n-1,2); // horizontal

Rect(x,y,n) <0.2> -> C(x,y);

C(x,y) <1/2> -> Coloredrect(1,1,1,x,y); // white
C(x,y) <1/18> -> Coloredrect(1,1,0,x,y); // yellow
C(x,y) <1/18> -> Coloredrect(1,0,0,x,y); // red
C(x,y) <1/18> -> Coloredrect(0,0,1,x,y); // blue
//C(x,y) <1/24> -> Coloredrect(0.5,0.5,0.5,x,y); // grey
C(x,y) <1/36> -> Coloredrect(0.1,0.1,0.1,x,y); // black


// no split for ratio 1
Rect2(x,y,n,type,ratio) : ratio == 1 -> Rect(x,y,n);

// vertical split
Rect2(x,y,n,type,ratio) : type == 1 && ratio < 1 -> [Rect(x,y*ratio,n)] Movexy(0,y*ratio) Rect(x,y*(1-ratio),n);

// horizontal split
Rect2(x,y,n,type,ratio) : type == 2 && ratio < 1 -> [Rect(x*ratio,y,n)] Movexy(x*ratio,0) Rect(x*(1-ratio),y,n);
